plugins {
  id "java"
  id "org.springframework.boot" version "2.3.3.RELEASE"
  id "io.spring.dependency-management" version "1.0.9.RELEASE"
  // TODO: check if required, can be achieved with config but may not be better.
  id "net.ltgt.apt" version "0.21"

  // Code Quality
  id "checkstyle"
  id "jacoco"
  id "org.sonarqube" version "3.0"
}

group = "uk.nhs.hee.trainee.details"
version = "0.0.2"
sourceCompatibility = "11"

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot
  implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: "org.junit.vintage", module: "junit-vintage-engine"
  }

  // Lombok
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"

  // Mapstruct
  implementation "org.mapstruct:mapstruct:1.3.1.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

  // Swagger
  implementation "io.springfox:springfox-swagger2:2.9.2"
  implementation "io.springfox:springfox-swagger-ui:2.9.2"
}

checkstyle {
  toolVersion = "8.26"
  config = resources.text.fromArchiveEntry(configurations.checkstyle[0], "google_checks.xml")
}

jacocoTestReport {
  reports {
    html.enabled true
    xml.enabled true
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", "health-education-england"
    property "sonar.projectKey", "Health-Education-England_TIS-TRAINEE-DETAILS"

    property "sonar.java.checkstyle.reportPaths",
      "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
  }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}
